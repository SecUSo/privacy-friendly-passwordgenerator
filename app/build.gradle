apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    defaultConfig {
        applicationId "org.secuso.privacyfriendlypasswordgenerator"
        minSdkVersion 21
        targetSdkVersion 34
        compileSdk 34
        versionCode 9
        versionName "1.3.2"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        buildConfig true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    kotlin {
        jvmToolchain(17)
    }

    android.applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            def appName = "pfa-passwordgenerator"
            outputFileName = appName + "-${variant.name}-v${variant.versionName}.apk"
        }
    }

    lint {
        lintConfig = file("lint.xml")
    }
    namespace 'org.secuso.privacyfriendlypasswordgenerator'
}

repositories {
    maven {
        url 'https://dl.bintray.com/amulyakhare/maven'
    }
}

dependencies {
    implementation project(path: ':backup-api')
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.sqlite:sqlite:2.4.0'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.5.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:3.0.2') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.github.amulyakhare:TextDrawable:558677ea31'
    implementation 'androidx.security:security-crypto-ktx:1.1.0-alpha06'

    def work_version = "2.9.1"
    implementation "androidx.work:work-runtime:$work_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    androidTestImplementation "androidx.work:work-testing:$work_version"

    testImplementation 'junit:junit:4.13.2'

    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}

tasks.withType(Test) {
    testLogging {
        events "passed", "skipped", "failed", "standard_out"
        exceptionFormat "full"
        afterSuite { desc, result ->
            if (!desc.parent) {
                println("\nResult: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)\n")
            }
        }
    }
}